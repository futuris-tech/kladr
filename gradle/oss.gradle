apply plugin: "signing"
apply plugin: "maven"


def isReleaseBuild = hasProperty("release")
def isSnapshotBuild = hasProperty("snapshot")
def isDevBuild = (!isReleaseBuild && !isSnapshotBuild)

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from docsDir
}

task sourcesJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

if (isReleaseBuild) {
    signing { sign configurations.archives }
} else {
    task signArchives { /* do nothing */ }
}

uploadArchives {

    repositories.mavenDeployer {
        configuration = configurations.archives
        uniqueVersion = false

        if (!isReleaseBuild) {
            pom.version = project.version + "-SNAPSHOT"
        }

        if (isDevBuild) {
            repository(url: "file://${System.getProperty('user.home')}/.m2/repository/")
        } else {
            if (isReleaseBuild) {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypeUsername)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
        }
    }
}
